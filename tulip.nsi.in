!include "MUI.nsh"
; no more env variables needed
;!include "WriteEnvStr.nsh"

;General

Name "Tulip @VERSION@"
OutFile "TulipSetup_@WIN_EXE_VERSION@.exe"

;--------------------------------
;Pages

!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "@prefix@\lib\tlp\bitmaps\headerlogo.bmp"
!define MUI_HEADERIMAGE_UNBITMAP "@prefix@\lib\tlp\bitmaps\headerlogo.bmp"
!define MUI_WELCOMEFINISHPAGE_BITMAP "@prefix@\lib\tlp\bitmaps\welcomelogo.bmp"
!define MUI_WELCOMEFINISHPAGE_BITMAP_NOSTRETCH
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "@prefix@\lib\tlp\bitmaps\welcomelogo.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP_NOSTRETCH

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "COPYING.LESSER"

!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH
!insertmacro MUI_LANGUAGE "English" 

; check for previous version
Function CheckPreviousVersion
  ReadRegStr $R0 HKLM Software\Microsoft\Windows\CurrentVersion\Uninstall\Tulip UninstallString
  StrCmp $R0 "" CheckPrev_no
  MessageBox MB_OK|MB_ICONSTOP "A previous version of Tulip was found.$\n$\nYou have to uninstall it before installing this new one."
  Abort
CheckPrev_no: 
FunctionEnd

Function GetParentDir
  Exch $R0
  Push $R1
  Push $R2
  Push $R3
   
  StrCpy $R1 0
  StrLen $R2 $R0
   
  loop:
    IntOp $R1 $R1 + 1
    IntCmp $R1 $R2 get 0 get
    StrCpy $R3 $R0 1 -$R1
    StrCmp $R3 "\" get
    Goto loop

  get:
    StrCpy $R0 $R0 -$R1
    
    Pop $R3
    Pop $R2
    Pop $R1
    Exch $R0
FunctionEnd

; initialization
Function .onInit
InitPluginsDir ;on initialise le r�ertoire de d�arrage des plugins
File /oname=$PLUGINSDIR\splash.bmp "@prefix@\lib\tlp\bitmaps\logo.bmp" ;on copie l'image dans l'exe final et on on la met dans le dossier temporaire.
advsplash::show 1800 350 187 2 $PLUGINSDIR\splash
Pop $R0 ;on r�up�e la valeur de retour : 1, si l'utilisateur a ferm�l'�ran, 0 si tout s'est bien pass� et -1 si un probl�e est survenu
Delete $PLUGINSDIR\spltmp.bmp ;idem pour l'image

; we try to avoid to have a space in the install dir path
; in order to ease the compilation of customer plugins using g++
; so we remove Program Files from the proposed installation path
Push $PROGRAMFILES
Call GetParentDir
Pop $R0
StrCpy $INSTDIR "$R0\Tulip\"

Call CheckPreviousVersion
FunctionEnd

Section "-Programs (required)"
SetOutPath $INSTDIR
FILE @prefix@\lib\tlp\bitmaps\logo32x32.ico
FILE "${NSISDIR}\Contrib\Graphics\Icons\win-uninstall.ico"
File @prefix@\bin\*.exe
File @prefix@\bin\tulip-config
File "@MINDIR@\bin\iconv.dll"
File "@MINDIR@\bin\zlib1.dll" 
File "@MINDIR@\bin\freetype*.dll"
File "@MINDIR@\bin\glew*.dll"
File "@MINDIR@\bin\libxml*.dll"
File "@MINDIR@\bin\jpeg*.dll"
File "@MINDIR@\bin\libpng*.dll"
File "@MINDIR@\bin\mingwm*.dll"
File "@qt_libraries@\..\..\mingw\bin\libgcc_s_dw2-1.dll"
File "@qt_libraries@\..\..\mingw\bin\libstdc++-6.dll"
File "@qt_libraries@\..\..\mingw\bin\libgomp-1.dll"
File "@qt_libraries@\..\..\mingw\bin\pthreadGC2.dll"
File "@qt_libraries@\..\bin\QtCore4.dll"
File "@qt_libraries@\..\bin\QtGui4.dll"
File "@qt_libraries@\..\bin\QtNetWork4.dll"
File "@qt_libraries@\..\bin\QtOpenGL4.dll"
File "@qt_libraries@\..\bin\QtXml4.dll"
File "@qt_libraries@\..\bin\QtAssistantClient4.dll"
File "@qt_libraries@\..\bin\QtScript4.dll"
File "@qt_libraries@\..\bin\QtSql4.dll"
File "@qt_libraries@\..\bin\QtWebkit4.dll"
File "@qt_libraries@\..\bin\QtXmlPatterns4.dll"
File "@qt_libraries@\..\bin\phonon4.dll"
File "@qt_libraries@\..\bin\@QT_ASSISTANT@.exe"
File "@prefix@\bin\libtulip-@WIN_VERSION@.dll" 
File "@prefix@\bin\libtulip-ogl-@WIN_VERSION@.dll" 
File "@prefix@\bin\libtulip-qt4-@WIN_VERSION@.dll" 
File "@prefix@\bin\libtulip-pluginsmanager-@WIN_VERSION@.dll" 
setOutPath $INSTDIR\imageformats
File "@qt_libraries@\..\plugins\imageformats\*.dll"
setOutPath $INSTDIR\lib\tlp\bitmaps
File @prefix@\lib\tlp\bitmaps\*
SectionEnd

Section "-Install Plugins and Docs"
setOutPath $INSTDIR\lib\tlp
; autotools
File /nonfatal @prefix@\lib\bin\*-@VERSION@.dll
; cmake
File /nonfatal @prefix@\lib\tlp\*-@VERSION@.dll
setOutPath $INSTDIR\lib\tlp\glyphs
; autotools
File /nonfatal @prefix@\lib\tlp\bin\lib*-@VERSION@.dll
; cmake
File /nonfatal @prefix@\lib\tlp\glyphs\lib*-@VERSION@.dll
setOutPath $INSTDIR\lib\tlp\view
; autotools
File /nonfatal @prefix@\lib\tlp\bin\libSpreadSheet-@VERSION@.dll
; cmake
File /nonfatal @prefix@\lib\tlp\view\libSpreadSheet-@VERSION@.dll
setOutPath $INSTDIR\lib\tlp\designer
; autotools
File /nonfatal @prefix@\lib\tlp\bin\*-@WIN_VERSION@.dll
; cmake
File /nonfatal @prefix@\lib\tlp\designer\*-@WIN_VERSION@.dll
setOutPath $INSTDIR\share
; Try to save doc files if any
File /nonfatal /r @prefix@\share\*
SectionEnd


Section "Shortcuts" raccourcis
SetOutPath "$SMPROGRAMS\Tulip @VERSION@"
CreateShortCut "$SMPROGRAMS\Tulip @VERSION@\Tulip.lnk" "$INSTDIR\Tulip.exe" "" "$INSTDIR\logo32x32.ico"
CreateShortCut "$SMPROGRAMS\Tulip @VERSION@\Uninstall.lnk" "$INSTDIR\uninst-Tulip.exe" "" "$INSTDIR\win-uninstall.ico"
CreateShortCut "$DESKTOP\Tulip @VERSION@.lnk" "$INSTDIR\Tulip.exe" "" "$INSTDIR\logo32x32.ico"
SectionEnd

Section "SDK" sdk
setOutPath $INSTDIR\include\tulip
File @prefix@\include\tulip\*.h
setOutPath $INSTDIR\include\tulip\cxx
File @prefix@\include\tulip\cxx\*.cxx
SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${raccourcis} "Create a shortcut on the Desktop, and in the start menu"
	!insertmacro MUI_DESCRIPTION_TEXT ${sdk} "Install the sdk version, install the include files in the Tulip directory"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Section -PostInstall
WriteRegStr HKLM Software\Microsoft\Windows\CurrentVersion\Uninstall\Tulip DisplayName "Tulip @VERSION@ (uninstall)"
WriteRegStr HKLM Software\Microsoft\Windows\CurrentVersion\Uninstall\Tulip UninstallString '"$INSTDIR\uninst-Tulip.exe"'
WriteUninstaller "uninst-Tulip.exe"
WriteRegStr HKLM "Software\Tulip" "Directory" '"$INSTDIR"'
WriteRegStr HKLM "Software\Tulip" "Version" "@VERSION@"
SectionEnd

UninstallText "Do you really want to remove Tulip @VERSION@ from your system ?"

Section "Uninstall"
DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tulip"
DeleteRegKey HKLM "Software\Tulip"
Delete "$INSTDIR\*.exe"
Delete "$INSTDIR\tulip-config"
Delete "$INSTDIR\*.ico"
Delete "$DESKTOP\Tulip @VERSION@.lnk"
Delete "$SMPROGRAMS\Tulip @VERSION@\*.*"
RMDir "$SMPROGRAMS\Tulip @VERSION@"
Delete "$INSTDIR\*.dll"
Delete "$INSTDIR\imageformats\*.dll"
RMDir "$INSTDIR\imageformats"
Delete "$INSTDIR\lib\tlp\bitmaps\*"
RMDir "$INSTDIR\lib\tlp\bitmaps"
Delete "$INSTDIR\lib\tlp\designer\*"
RMDir "$INSTDIR\lib\tlp\designer"
Delete "$INSTDIR\lib\tlp\glyphs\*"
RMDir "$INSTDIR\lib\tlp\glyphs"
Delete "$INSTDIR\lib\tlp\view\*"
RMDir "$INSTDIR\lib\tlp\view"
Delete "$INSTDIR\lib\tlp\toInstall\*"
RMDir "$INSTDIR\lib\tlp\toInstall"
Delete "$INSTDIR\lib\tlp\*"
RMDir "$INSTDIR\lib\tlp"
Delete "$INSTDIR\lib\*"
RMDir "$INSTDIR\lib"
Delete "$INSTDIR\include\tulip\cxx\*"
RMDir "$INSTDIR\include\tulip\cxx"
Delete "$INSTDIR\include\tulip\*"
RMDir "$INSTDIR\include\tulip"
RMDir "$INSTDIR\include"
IfFileExists "$INSTDIR\share\*.*" 0 +2
RMDir /r "$INSTDIR\share"
RMDir "$INSTDIR"
SectionEnd
